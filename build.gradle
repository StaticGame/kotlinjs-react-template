/*
 * This file is intended to be used as starter for kotlinJs 'React purpose' builds
 *
 * It should contain all the necessary dependencies (kotlin standard, base react integration and javascript) to start
 * tweaking react based project
 */
plugins {
    id "org.jetbrains.kotlin.frontend" version "0.0.30"
    id "kotlin2js" version "1.2.31"
    id "kotlin-dce-js" version "1.2.31"
}

ext.kotlinVersion = "1.2.31"
ext.kotlinCoroutineVersion = "0.22.1"
ext.kotlinxHtmlVersion = "latest-1.2.40-dev-769-337"
ext.wrapperReactVersion="16.3.1-pre.27-kotlin-1.2.30"
ext.wrapperExtensionsVersion = "1.0.1-pre.27-kotlin-1.2.30"


group = "local.playground"
version = "0.0.1-SNAPSHOT"

repositories {
    /*
     * Repository for kotlin-wrappers (react, react-dom, react-router, redux)
     */
    maven { url = "http://dl.bintray.com/kotlin/kotlin-js-wrappers/" }
    /*
     * kotlinx-html-js repository (0.6.9)
     */
    maven { url = "http://dl.bintray.com/kotlin/kotlinx.html/" }
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$kotlinCoroutineVersion"

    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-test-js:$kotlinVersion"

    compile "org.jetbrains:kotlin-extensions:$wrapperExtensionsVersion"
    compile "org.jetbrains:kotlin-react:$wrapperReactVersion"
    compile "org.jetbrains:kotlin-react-dom:$wrapperReactVersion"

}

compileKotlin2Js {
    kotlinOptions {
        outputFile = "${buildDir}/kotlin2js/bundle.js"
        sourceMap = true
        moduleKind = "commonjs"
    }
}

runDceKotlinJs {
    dceOptions {
//        devMode = false      // Boolean
//        outputDirectory = "" //String?
    }
}

kotlinFrontend {

    downloadNodeJsVersion = "8.10.0"
//    nodeJsMirror

    sourceMaps = true

    npm {
        dependency("react", "16.3.1")
        dependency("react-dom", "16.3.1")
        dependency("react-transition-group", "2.3.1")

        devDependency("css-loader")
        devDependency("style-loader")
        devDependency("sass-loader")
        devDependency("file-loader")
        devDependency("node-sass")
        devDependency("babel-loader")
        devDependency("babel-core")
        devDependency("karma")
        devDependency("html-webpack-plugin")
    }

    webpackBundle {
        port = 8080
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

task copyWeb(type: Copy) {
    from "${sourceSets.main.output.resourcesDir}/web"
    into "${buildDir}/kotlin-js-min/main/web"
}

copyWeb.dependsOn(processResources)
runDceKotlinJs.dependsOn(copyWeb)
